plugins {
  id 'com.android.library'
  id 'jacoco'
}

jacoco {
  toolVersion = '0.8.10'
}

android {
  namespace 'org.smartregister.immunization'
  compileSdk 35
  buildToolsVersion '35.0.0'

  defaultConfig {
    minSdk 28
    targetSdk 35
    versionCode Integer.parseInt(project.VERSION_CODE)
    versionName project.VERSION_NAME
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    debug {
      enableUnitTestCoverage = true
      buildConfigField 'int', 'VACCINE_SYNC_TIME', '12'
    }
    release {
      minifyEnabled = false
      proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
      buildConfigField 'int', 'VACCINE_SYNC_TIME', '12'
    }
  }

  lint {
    abortOnError = false
  }

  packaging {
    resources {
      excludes += [
        'LICENSE.txt',
        'META-INF/LICENSE',
        'META-INF/LICENSE.txt',
        'META-INF/license.txt',
        'META-INF/NOTICE',
        'META-INF/NOTICE.txt',
        'META-INF/notice.txt',
        'META-INF/ASL2.0',
        'META-INF/DEPENDENCIES',
        'META-INF/DEPENDENCIES.txt',
        'META-INF/INDEX.LIST'
      ]
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      returnDefaultValues = true
      all {
        maxHeapSize = '1024m'
        jvmArgs(
          '--add-opens=java.base/java.lang=ALL-UNNAMED',
          '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
          '--add-opens=java.base/java.io=ALL-UNNAMED',
          '--add-opens=java.base/java.net=ALL-UNNAMED',
          '--add-opens=java.base/java.util=ALL-UNNAMED',
          '--add-opens=java.base/java.text=ALL-UNNAMED',
          '--add-opens=java.base/java.security=ALL-UNNAMED',
          '--add-opens=java.base/jdk.internal.reflect=ALL-UNNAMED',
          '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
          '--add-opens=java.base/jdk.internal.util=ALL-UNNAMED',
          '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
          '--add-opens=java.base/sun.security.util=ALL-UNNAMED',
          '--add-opens=java.desktop/java.awt.font=ALL-UNNAMED',
          '--add-exports=java.base/jdk.internal.reflect=ALL-UNNAMED'
        )
        systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
        systemProperty 'robolectric.enabledSdks', '28'
      }
    }
  }

  publishing {
    singleVariant('release') {
      withSourcesJar()
      withJavadocJar()
    }
  }
}

tasks.withType(Test).configureEach {
  jacoco {
    includeNoLocationClasses = true
    excludes = ['jdk.internal.*']
  }
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

  implementation('io.github.bluecodesystems:opensrp-client-core:6.2.2') {
    transitive = true
  }
  implementation('io.github.bluecodesystems:opensrp-client-native-form-new:3.1.3') {
    transitive = true
  }
  implementation('com.github.BlueCodeSystems:opensrp-client-configurable-views:v2.0.1') {
    transitive = true
  }
  implementation 'io.github.bluecodesystems:opensrp-plan-evaluator:1.7.0'

  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.recyclerview:recyclerview:1.3.2'
  implementation 'com.google.android.material:material:1.12.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'org.apache.commons:commons-lang3:3.13.0'
  implementation 'com.evernote:android-job:1.4.3'
  implementation 'net.zetetic:android-database-sqlcipher:4.5.4'
  implementation 'com.google.guava:guava:31.1-android'
  implementation 'androidx.sqlite:sqlite:2.3.1'
  implementation 'androidx.sqlite:sqlite-framework:2.3.1'
  implementation 'com.google.code.gson:gson:2.10.1'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.mockito:mockito-core:5.2.0'
  testImplementation 'org.mockito:mockito-inline:5.2.0'
  testImplementation 'org.robolectric:robolectric:4.10.3'
  testImplementation 'androidx.sqlite:sqlite:2.3.1'
  testImplementation 'androidx.test:core:1.5.0'
  testImplementation 'net.zetetic:android-database-sqlcipher:4.5.4'
  testImplementation 'org.json:json:20240303'
  testImplementation 'org.skyscreamer:jsonassert:1.5.1'

  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
  androidTestImplementation 'androidx.test:rules:1.5.0'
}

tasks.register('jacocoTestReport', JacocoReport) {
  dependsOn 'testDebugUnitTest'
  reports {
    xml.required.set(true)
    xml.outputLocation.set(file("${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"))
    html.required.set(true)
    html.outputLocation.set(file("${buildDir}/reports/jacoco/jacocoRootReport/html"))
  }

  def fileFilter = ['**/R.class','**/R$*.class','**/BuildConfig.*','**/Manifest*.*','**/*Test*.*','android/**/*.*','**/*$ViewBinder*.*']
  def debugTree = fileTree(dir: "$buildDir/intermediates/javac/debug/classes", excludes: fileFilter)
  def mainSrc = "$projectDir/src/main/java"

  sourceDirectories.setFrom(files(mainSrc))
  classDirectories.setFrom(files(debugTree))
  executionData.setFrom(fileTree(dir: buildDir, includes: [
    'jacoco/testDebugUnitTest.exec',
    'outputs/code-coverage/connected/*coverage.ec'
  ]))
}

apply from: '../maven.gradle'
